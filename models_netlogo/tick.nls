;; ##### TICK FUNCTIONS #####

to update-health
  ask residents [
    let threshhold table:get health-table medical-treatment
    let difference health - threshhold
    set health health + difference * 0.01
  ]

  ask residents with [health < 0][
    set deaths deaths + 1
    ;; TODO check if in ambulance, if so update ambulance
    die
  ]
  ask residents with [health > 1][
    if medical-treatment = "hospital" [
      ask one-of hospitals-here [set occupancy occupancy - 1]
      if color = blue [set recovered-hospital recovered-hospital + 1]]
    ifelse medical-treatment = "none"
      [set recovered-unchecked recovered-unchecked + 1]
      [set recovered-with-help recovered-with-help + 1]

    die
  ]
end

to call-112
  ifelse call-limit? [
    ;; WITH a call-limit
    ask crossings
    [ if any? residents-here with [not reported?]
      [
        let randomizer random-float 1
        if randomizer <  1 - ( 1 - probability-call-112 / 100 ) ^ total-residents
        [ ask one-of residents-here [ set tries-calling? true] ] 
      ]
    ]
    let amount-callers count residents with [calling?]
    ask up-to-n-of ( max-concurrent-calls - amount-callers ) residents with [tries-calling?] [
      set calling? true
      set tries-calling? false
      set time-call random-poisson average-call-time  ;; TODO: Make Gamma-distribution
    ]
    ask residents with [calling?] [
      set time-call time-call - 1
      if time-call <= 0 [
        ask residents-here [set reported? true]
        set calling? false
      ]
    ]
  ] [
  ;; WITHOUT a call-limit
    ask crossings
    [ if any? residents-here with [not reported?]
      [
        let randomizer random-float 1
        if randomizer <  1 - ( 1 - probability-call-112 / 100 ) ^ total-residents
        [ ask residents-here [ set reported? true ] ]
      ]
    ]
  ]
end

to go-ambulances
  nw:set-context turtles with [part-of-network?] roads with [color != red]
  ask ambulances [
    ;; Check if a new patient needs to be requested, a patient can be picked up or if arrived at hospital
    if not enroute? [request-new-patient initial-ambulance-search-radius]
    if not full? and destination != nobody and member? destination crossings-here [pick-up-patient] 
    if full? and destination != nobody and member? destination hospitals-here [drop-off-patient]
    if enroute? and (ticks mod ambulance-reroute-frequency = 0 or route = false) [plan-route]  ;; Plan new route if no route and every n ticks
    if enroute? and route != false and length route >= 2 [move-ambulances]
  ]
end

to move-ambulances
  let target-link road [who] of item 0 route [who] of item 1 route
  let target-color [color] of target-link
  ifelse target-color != blue and target-color != red [
    move-to item 1 route
    set route remove-item 0 route
  ][
    ask target-link [set color red]
    plan-route
    if route != false and length route >= 2 [move-ambulances] ;; Note the recursive use of this function in the "else" branch
  ]
end

to request-new-patient [increased-radius]
  if not any? residents with [medical-treatment != "hospital"] [die]
  ;; Get resident with lowest health in search radius that is reported
  let target-patient nobody
  ask one-of turtles-here with [part-of-network?] [
    let potential-crossings nw:turtles-in-radius increased-radius
    set potential-crossings potential-crossings with [any? residents-here with [reported? and medical-treatment != "hospital" and not help-underway?]]
    let potential-patients residents-on potential-crossings
    set target-patient min-one-of potential-patients with [reported? and medical-treatment = "none" and not help-underway?] [health] ;;TODO think about checked
  ]
  set patient target-patient

  ifelse patient != nobody [
    set destination one-of crossings-on patient
    ask patient [set help-underway? true]
    plan-route
    set enroute? true
  ][
    ifelse increased-radius < 1900
      [let new-radius increased-radius * 1.5  ;; recursively increase the radius with 20% untill a patient is found
       request-new-patient new-radius]
      [if ticks > 100 [
         set find-counter find-counter + 1
         if find-counter > 5 [die]
      ]
    ]
  ]
end

to plan-route
  if destination != nobody [
    nw:set-context turtles with [part-of-network?] roads with [color != red]
    ask min-one-of turtles-here with [part-of-network?] [distance myself] [
      let potential-route nw:turtles-on-path-to [destination] of myself
      ask myself [set route potential-route]
    ]
  ]
  if route = false and not full? [request-new-patient initial-ambulance-search-radius]
  if route = false and full? [find-hospital]
end

to pick-up-patient
  if patient != nobody [ask patient [set help-underway? false]]                      ;; Make the previously targetet patient available for help again, incase (s)he doesn't get picked up
  set patient min-one-of residents-here with [medical-treatment != "ambulance" and medical-treatment != "hospital"] [health]   ;; Pick up the most injured patient
  
  ask residents-here with [medical-treatment = "none"] [
    set medical-treatment "checked"
  ]

  ifelse patient != nobody [
    ask patient [
      set medical-treatment "ambulance"
      set reported? true
    ]
    find-hospital
    plan-route
    set enroute? true
    set full? true
  ][
    request-new-patient initial-ambulance-search-radius
  ]
end

to find-hospital
  nw:set-context turtles with [part-of-network?] roads with [color != red]
  let current-crossing min-one-of crossings-here [distance myself]
  set destination min-one-of hospitals with [capacity > occupancy] [nw:distance-to current-crossing]
  if destination = nobody [
    set route false
  ]
end

to drop-off-patient ;; TODO: Implement what happens if resident died in the journey
  ifelse patient = nobody
  [
    set destination nobody
    set full? false
    set enroute? false
  ][
    ask patient [
      move-to [destination] of myself
      set medical-treatment "hospital"]
    ask destination [set occupancy occupancy + 1]
    set patient nobody
    set destination nobody
    set full? false
    set enroute? false
  ]
end

to update-hospitals
  ask hospitals [set label occupancy]
end

to operate-drones
  repeat 3 [
    ask drones [
      if not any? crossings with [not spotted?] [die]

      ;; Charge if battery is not full and drone is at home, otherwise fly
      if battery > drone-range and member? base-hospital hospitals-here [set flying? true]
    
      ifelse flying? [
        ifelse battery >= 0.5 * drone-range [
          ;; If battery is more than 50% full, fly to unspotted crossings
          face min-one-of crossings with [not spotted?] [distance myself]
          move-and-spot-broken-roads
        ] [
          ;; If battery is less then 50% full, fly home
          face base-hospital
          move-and-spot-broken-roads
          if distance base-hospital <= drone-speed * 20 [
            move-to base-hospital
            set flying? false
          ]
        ]
      ] [
        ;; When not flying, charge the battery
        set battery battery + 0.3333333 ;; 1 / 3
      ]
    ]
  ]
end

to move-and-spot-broken-roads
  forward drone-speed * 20 ;; This is 60 (seconds per minute) devided by the repeat 3
  set battery battery - 0.3333333 ;; 1 / 3
  ask crossings in-radius drone-view-radius [
    ask my-links with [color = blue] [set color red]
    set spotted? true
  ]
end